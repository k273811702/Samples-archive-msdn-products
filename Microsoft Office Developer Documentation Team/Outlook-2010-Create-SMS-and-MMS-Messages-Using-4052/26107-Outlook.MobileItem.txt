Option Explicit

' Outlook 2010

' Demonstrate the Outlook.MobileItem object.

' Use this sample to create an SMS message or MMS message from
' Outlook 2010.

' An Outlook MobileItem represents an SMS message or MMS message
' that you send to a mobile device like a phone.
'
' To actually send the message, you'll need to have configured
' Outlook with a Windows phone running 6.1 Update or 6.5.
'
' OR
'
' You can use an SMS service on the Internet.

' This example creates two messages: one SMS and one MMS.
' It then shows the Inspector for each item.
' You can then choose to provide a recipient or
' cancel the messages once the code completes execution.

' Open the VBA editor and paste this code into the existing
' ThisOutlookSession module.
' With the cursor inside the DemoMobileItem method, press F5.

Sub DemoMobileItem()
    Dim app As Outlook.Application
    Set app = ThisOutlookSession
    
   ' Change the following path to refer to a
   ' small picture on your computer.
   Const PICTURE_PATH As String = _
    "C:\Users\Public\Pictures\Sample Pictures\Desert.jpg"

    ' Create an SMS message and provide a body text value.
    Dim sms As Outlook.MobileItem
    Set sms = app.CreateItem(olMobileItemSMS)
    sms.Subject = "Test SMS"
    sms.Body = "Short Message Service (SMS) message from Outlook 2010."
    ' Display the Inspector non-modally.
    sms.Display False
    
    Dim mms As Outlook.MobileItem
    Set mms = app.CreateItem(olMobileItemMMS)
    mms.Subject = "Test MMS with picture of the desert"
    
    ' Attach the picture to the MMS.
    mms.Attachments.Add PICTURE_PATH
    ' Display the Inspector non-modally.
    mms.Display False
End Sub

