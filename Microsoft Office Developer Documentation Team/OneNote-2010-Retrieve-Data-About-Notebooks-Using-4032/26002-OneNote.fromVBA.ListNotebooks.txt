Option Explicit

' OneNote 2010

' Demonstrate the GetHierarchy method.

' Use any VBA host including Excel 2010, PowerPoint 2010,
' or Word 2010.
' OneNote 2010 is not a VBA host.

' In your VBA host, add references to the following
' external libraries using the Add References dialog:
' Microsoft OneNote 14.0 Object Library
' Microsoft XML, v6.0

' OneNote's GetHierarchy method allows you
' to get meta-data and data about the OneNote
' Notebooks.

' Paste all this code into a module,
' place the cursor within the
' ListOneNoteNotebooks procedure, and press F5.
'
' The ListOneNoteNotebooks procedure uses
' the MSXML library to parse the returned XML
' from OneNote and output Notebook metadata
' to the Immediate window of your VBA host.

Sub ListOneNoteNotebooks()
    ' Connect to OneNote 2010.
    ' OneNote will be started if it's not running.
    Dim oneNote As OneNote14.Application
    Set oneNote = New OneNote14.Application
    
    ' Get the XML that represents the OneNote notebooks available.
    Dim notebookXml As String
    
    ' notebookXml gets filled in with an XML document providing information
    ' about what OneNote notebooks are available.
    ' You want all the data. Thus you provide an empty string
    ' for the bstrStartNodeID parameter.
    oneNote.GetHierarchy "", hsNotebooks, notebookXml, xs2010
    
    ' Use the MSXML Library to parse the XML.
    Dim doc As MSXML2.DOMDocument
    Set doc = New MSXML2.DOMDocument
    
    If doc.LoadXML(notebookXml) Then
        ' Find all the Notebook nodes in the one namespace.
        Dim nodes As MSXML2.IXMLDOMNodeList
        Set nodes = doc.DocumentElement.SelectNodes("//one:Notebook")
            
        Dim node As MSXML2.IXMLDOMNode
        Dim noteBookName As String
        Dim temp As String
        ' Walk the collection of Notebooks.
        ' Read attribute values and write them
        ' out to the Immediate window of your VBA host.
        For Each node In nodes
            noteBookName = node.Attributes.getNamedItem("name").Text
            Debug.Print "Notebook Name and Location: "; vbCrLf & " " & _
                 noteBookName & " is at " & _
                node.Attributes.getNamedItem("path").Text
            Debug.Print "Additional data for " & noteBookName
            Debug.Print "  ID: " & node.Attributes.getNamedItem("ID").Text
            ' Not all notebooks will have all additional data below.
            Debug.Print "  Color: " & GetAttributeValueFromNode(node, "color")
            Debug.Print "  Is Unread: " & GetAttributeValueFromNode(node, "isUnread")
            Debug.Print "  Last Modified: " & GetAttributeValueFromNode(node, "lastModifiedTime")
        Next
    Else
        MsgBox "OneNote 2010 XML Data failed to load."
    End If
    
End Sub

Private Function GetAttributeValueFromNode(node As MSXML2.IXMLDOMNode, attributeName As String) As String
    If node.Attributes.getNamedItem(attributeName) Is Nothing Then
        GetAttributeValueFromNode = "Not found."
    Else
        GetAttributeValueFromNode = node.Attributes.getNamedItem(attributeName).Text
    End If
End Function