' Visio 2010 makes it easy to add a visual boundary around shapes, 
' using the new feature, Containers.
' The following code creates a new Visio document and adds
' two rectangles to the first page of the document.
' It then creates a Container object and adds the
' two rectangles to the Container.
' Finally, the code changes the default text of the container.
' To run this demo, open Visio 2010 and create a blank document.
' Open the VBA editor and paste this code into the existing ThisDocument module. 
' With the cursor inside this method, press F5.

Sub AddShapesToAContainer()
    ' Access objects on the current document.
    Dim doc As Visio.Document
    Set doc = Application.ActiveDocument
    Dim thePage As Visio.Page
    Set thePage = doc.Pages(1)
    
    ' Open the built-in Basic Shapes stencil.
    ' If the stencil is already open, the code
    ' activates the stencil.
    Dim stencil As Visio.Document
    Dim stencilName As String
    stencilName = "Basic Shapes (US units).vss"
    Set stencil = Application.Documents.OpenEx(stencilName, visOpenDocked)
    
    ' Create a rectangle master shape from the stencil.
    Dim rectangleMaster As Visio.Master
    Set rectangleMaster = stencil.Masters("Rectangle")
    
    ' Define and create two rectangles on the document's first page.
    Dim rectangle1 As Visio.Shape
    Dim rectangle2 As Visio.Shape
    Set rectangle1 = thePage.Drop(rectangleMaster, 2, 10)
    Set rectangle2 = thePage.Drop(rectangleMaster, 2, 9)
    
    ' Select the two rectangles.
    ' Note: If the document already contains other shapes,
    ' they will also be selected.
    ActiveWindow.SelectAll
    
    ' Access the new Containers collection by opening the built-in
    ' stencil file for containers. You can access these objects in the
    ' Visio user interface by selecting the Insert tab.
    Dim containersDoc As Visio.Document
    Set containersDoc = Application.Documents.OpenEx( _
        Application.GetBuiltInStencilFile(visBuiltInStencilContainers, _
        visMSUS), visOpenHidden)
    
    ' Finally, use the new DropContainer method to add
    ' the container specified by the containerToAdd to the page.
    ' As part of adding the container to the page, the code adds 
    ' the selected items to the container.
    ' The code ends by setting the container's text property and 
    ' then cleans up.
    Dim containerToAdd As String
    containerToAdd = "Container 2"
    Dim containerShape As Visio.Shape
    Set containerShape = thePage.DropContainer( _
        containersDoc.Masters.ItemU(containerToAdd), _
        Application.ActiveWindow.Selection)
    containerShape.Text = "Two Rectangles"
    
    containersDoc.Close
    ActiveWindow.DeselectAll
End Sub

 

