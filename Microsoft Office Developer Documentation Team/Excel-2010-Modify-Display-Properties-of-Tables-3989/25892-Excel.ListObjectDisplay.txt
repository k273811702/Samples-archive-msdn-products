' Excel 2007/2010
' Working with Tables (ListObject)
' Modify display properties of a table.

' In Excel 2010, in a new workbook, copy all this code into
' the Sheet1 class module. Place the cursor in the
' ListObjectDisplayDemo procedure, and then press F8 to start debugging, then Shift+F8 to
' single-step through the code (stepping over any called procedures).
' Arrange the VBA and Excel windows
' side by side so you can see the results of running the code.

' Work with ListObject members:
'  ShowHeaders
'  ShowTableStyleColumnStripes
'  ShowTableStyleFirstColumn
'  ShowTableStyleLastColumn
'  TableStyle

Sub ListObjectDisplayDemo()
    ' Step over this procedure. It's not terribly interesting.
    FillRandomData
   
    Dim lo As ListObject
    Set lo = ListObjects.Add( _
     SourceType:=xlSrcRange, _
     Source:=Range("A1:F13"), _
     XlListObjectHasHeaders:=xlYes)
    lo.Name = "SampleData"
   
    ' See the ListTableStyles procedure to create a list of named styles:
    lo.TableStyle = "TableStyleLight2"
   
    ' Single step through the following code to see the behavior:
    lo.ShowHeaders = False
    lo.ShowHeaders = True
   
    lo.ShowTableStyleColumnStripes = True
    lo.ShowTableStyleColumnStripes = False
   
    lo.ShowTableStyleFirstColumn = True
    lo.ShowTableStyleFirstColumn = False
   
    lo.ShowTableStyleLastColumn = True
    lo.ShowTableStyleLastColumn = False
   
    lo.Comment = "This is a sample table. Check the Formulas|Name Manager to see the comment."
   
    ' Delete the List object so you can run the code again if you like.
    lo.Delete
End Sub

Sub FillRandomData()
    ' No need to stop through this procedure.
    Range("A1", "F1").Value = Array("Month", "North", "South", "East", "West", "Total")
   
    ' Fill in twelve rows with random data.
    Dim i As Integer
    Dim j As Integer
    For i = 1 To 12
        Cells(i + 1, 1).Value = MonthName(i, True)
        For j = 2 To 5
            Cells(i + 1, j) = Round(Rnd * 100)
        Next j
    Next i
    Range("F2", "F13").Formula = "=SUM(B2:E2)"
End Sub

Sub ListTableStyles()
    ' Use this procedure to list all the named table styles in the
    ' Immediate window. Put the cursor in this procedure and press F5
    ' to run it. Look in the Immediate window for the results.
    Dim ts As TableStyle
    For Each ts In ActiveWorkbook.TableStyles
        Debug.Print ts.Name
    Next ts
End Sub