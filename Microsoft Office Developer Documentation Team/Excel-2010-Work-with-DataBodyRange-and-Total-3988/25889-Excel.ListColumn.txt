' Excel 2007/2010
' Working with Tables (ListColumn)
' Work with DataBodyRange and Total properties

' In Excel 2010, in a new workbook, copy all this code into
' the Sheet1 class module. Place the cursor in the
' ListColumnDemo procedure, and then press F8 to start debugging, then Shift+F8 to
' single-step through the code (stepping over any called procedures).
' Arrange the VBA and Excel windows
' side by side so you can see the results of running the code.

Sub ListColumnDemo()
    ' Step over this procedure. It's not terribly interesting.
    FillRandomData
 
    ' Set up the list object.
    Dim lo As ListObject
    Set lo = ListObjects.Add( _
     SourceType:=xlSrcRange, _
     Source:=Range("A1:F13"), _
     XlListObjectHasHeaders:=xlYes)
    lo.Name = "SampleData"
    lo.ShowTotals = True
 
    ' Retrieve a reference to the second column in the ListObject:
    Dim lc As ListColumn
    Set lc = lo.ListColumns(2)
   
    ' Retrieve a reference to the ListColumn's data, excluding
    ' headers and footers.
    Dim rng As Range
    Set rng = lc.DataBodyRange
   
    ' Do some things with the data body range:
    rng.Borders.Color = vbRed
    rng.FormatConditions.AddDatabar
   
    ' Retrieve the total for the column and
    ' display it in the totals row.
    ' Obviously, there are other ways to do this:
    Dim totalValue As Long
    totalValue = WorksheetFunction.Sum(rng)
    ' You should verify that the Total property isn't
    ' Nothing--it will be, if the list object isn't
    ' displaying its totals row. In this case, the totals
    ' row is definitely visible, so this check is extraneous.
    If Not lc.Total Is Nothing Then
        lc.Total.Value = totalValue
    End If
End Sub

Sub FillRandomData()
    ' No need to stop through this procedure.
    Range("A1", "F1").Value = Array("Month", "North", "South", "East", "West", "Total")
 
    ' Fill in twelve rows with random data.
    Dim i As Integer
    Dim j As Integer
    For i = 1 To 12
        Cells(i + 1, 1).Value = MonthName(i, True)
        For j = 2 To 5
            Cells(i + 1, j) = Round(Rnd * 100)
        Next j
    Next i
    Range("F2", "F13").Formula = "=SUM(B2:E2)"
End Sub
